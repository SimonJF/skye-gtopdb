open MonadicParser;

# A simple parser for the GtoPdb markup.
# Limitations right now:
#  1. Cannot handle '<' and '>' characters which do not refer to tags
#  2. Doesn't handle HTML attributes yet

typename Markup = [|
    MarkupEmpty
  | MarkupAppend:(Markup, Markup)
  | MarkupText:String
  | MarkupReferenceTag:Int
  | MarkupLigandTag:Int
  | MarkupHTMLTag:(String, Markup)
|];

typename ParseResult = [| OK:Markup | Error:String |];

fun mkHtmlTag(tagName, contents) {
  MarkupHTMLTag(tagName, contents)
}

# Useful parser combinators for us

# streamChar: runs of characters that are not used to start or end tags.
var streamChar = sat(fun(c) { implode([c]) =~ /[^<>]/ });

var textStream =
  many1(streamChar) >>=
  fun(str) { return(MarkupText(implode(str))) };

fun htmlTag() {
  word() >>= fun (tagName) {
  char('>') >>
  markup() >>= fun (contents) {
  stringE("</") >>
  string(tagName) >>
  char('>') >>
  return(mkHtmlTag(implode(tagName), contents))
  }}
}

fun brTag() {
  (stringE("br>") ++!
   stringE("br/>") ++!
   stringE("br />")) >>
  return(mkHtmlTag("br", MarkupEmpty))
}

fun referenceTag() {
  stringE("MarkupReference") >>
  spaces() >>
  stringE("id") >>
  char('=') >>
  nat >>= fun(id) {
  stringE("/>") >>
  return(MarkupReferenceTag(id))
  }
}

fun ligandTag() {
  stringE("Ligand") >>
  spaces() >>
  stringE("id") >>
  char('=') >>
  nat >>= fun(id) {
  stringE("/>") >>
  return(MarkupLigandTag(id))
  }
}

fun tag() {
  char('<') >>
  brTag() ++! referenceTag() ++! ligandTag() ++! htmlTag()
}

sig markup : () {}~> Parser(Markup)
fun markup() {

  fun markupInner() {
    tag() ++!
    textStream
    #(char('<') >> return(MarkupText("<"))) ++!
    #(char('>') >> return(MarkupText(">")))
  }

  many(markupInner()) >>= fun(xs) {
    return(fold_right(fun(x, acc) { MarkupAppend(x, acc) }, MarkupEmpty, xs))
  }
}

fun parse(str) {
  switch((markup())(explode(str))) {
    case [] -> Error("Nothing matched.")
    case [(markup, [])] -> OK(markup)
    case [(markup, xs)] -> Error("Unconsumed input: " ^^ implode(xs))
    case xs -> Error("Ambiguous parse: " ^^ (show(xs)))
  }
}


var testStr =
  "Cannabinoid receptors (<b>nomenclature as agreed by the <u>NC-IUPHAR</u> Subcommittee on Cannabinoid Receptors <Reference id=14916/></b>) are activated by endogenous ligands that include N-arachidonoylethanolamine (<Ligand id=2364/>), <Ligand id=5444/>, <Ligand id=5445/> and <Ligand id=729/>. Potency determinations of endogenous agonists at these receptors are complicated by the possibility of differential susceptibility of endogenous ligands to enzymatic conversion <Reference
  id=13691/>.<br><br>There are currently three licenced cannabinoid medicines each of which contains a compound that can activate CB<sub>1</sub> and CB<sub>2</sub> receptors <Reference id=32075/>. Two of these medicines were developed to suppress nausea and vomiting produced by chemotherapy. These are <Ligand id=9071/> (Cesamet&reg;), a synthetic CB<sub>1</sub>/CB<sub>2</sub> receptor agonist, and synthetic <Ligand id=2424/> (Marinol&reg;; dronabinol), which can also be used as an appetite
  stimulant. The third medicine, Sativex&reg;, contains mainly <Ligand id=2424/> and <Ligand id=4150/>, both extracted from cannabis, and is used to treat multiple sclerosis and cancer pain.";

#parse("<b>hello, world!</b>")
parse(testStr)
