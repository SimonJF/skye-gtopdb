import Tables;

typename ObjectType = [|
  GPCR | IC | NHR | Enzyme | CatalyticReceptor | Transporter | Other |];


fun showObjectType(it) {
  switch(it) {
    case GPCR -> "GPCR"
    case IC -> "IC"
    case NHR -> "Nuclear Hormone Receptor"
    case Enzyme -> "Enzyme"
    case CatalyticReceptor -> "Catalytic Receptor"
    case Transporter -> "Transporter"
    case Other -> "Other"
  }
}

fun classifyObject(objectID) {
  fun inTable(tbl) {
    query {
      length(
        for (x <-- tbl)
        where (x.object_id == objectID)
          [x]) > 0
    }
  }

  if (inTable(Tables.gpcr)) {
    GPCR
  } else if (inTable(Tables.vgic) || inTable(Tables.lgic) || inTable(Tables.other_ic)) {
    IC
  } else if (inTable(Tables.nhr)) {
    NHR
  } else if (inTable(Tables.enzyme)) {
    Enzyme
  } else if (inTable(Tables.catalytic_receptor)) {
    CatalyticReceptor
  } else if (inTable(Tables.transporter)) {
    Transporter
  } else {
    Other
  }
}
