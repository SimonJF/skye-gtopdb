# Functionality for dealing with ligand<->object interactions.
# This will grow as I abstract more out.
open import MvuHTML;
open import Parser;
open import Tables;

typename InteractionType = [|
  GPCR | IC | NHR | Enzyme | CatalyticReceptor | Transporter | Other
  | TargetLigand
|];

fun showInteractionType(it) {
  switch(it) {
    case GPCR -> "GPCR"
    case _ -> "TODO"
  }
}

typename InteractionAffinityVoltage =
  (affinityVoltageHigh: Float,
   affinityVoltageMedian: Float,
   affinityVoltageLow: Float,
   affinityPhysiologicalVoltage: Bool);

typename InteractionAffinity =
  (affinityUnits: String, affinityLow: Float,
   affinityMedian: Float, affinityHigh: Float);

fun mkAffinity(units, high, median, low) {
  (affinityHigh = high, affinityMedian = median, affinityLow = low,
   affinityUnits = units)
}

fun mkAffinityVoltage(high, median, low, physio) {
  (affinityVoltageHigh = high, affinityVoltageMedian = median, affinityVoltageLow = low,
   affinityPhysiologicalVoltage = physio)
}

typename Interaction =
  (interactionID: Int,
   ligandID: Int,
   objectID: Int,
   targetName: String,
   interactionType: InteractionType,
   interactionAction: String,
   interactionActionComment: String,
   speciesID: Int,
   endogenous: Bool,
   selective: Bool,
   useDependent: Bool,
   voltageDependent: Bool,
   affinity: InteractionAffinity,
   concentrationRange: String,
   affinityVoltage: InteractionAffinityVoltage,
   rank: Int,
   selectivity: String,
   originalAffinity: InteractionAffinity,
   originalAffinityRelation: String,
   assayDescription: String,
   assayConditions: String,
   fromGrac: Bool,
   onlyGrac: Bool,
   receptorSite: String,
   ligandContext: String,
   percentActivity: Float,
   assayURL: String,
   primaryTarget: Bool,
   targetLigandID: Int,
   wholeOrganismAssay: Bool,
   hide: Bool,
   references: [Int]);

fun classifyInteractionType(objectID, targetLigandID) {
  if (targetLigandID > 0) {
    TargetLigand
  } else {
    fun inTable(tbl) {
      query {
        length(
          for (x <-- tbl)
          where (x.object_id == objectID)
            [x]) > 0
      }
    }
    if (inTable(Tables.gpcr)) {
      GPCR
    } else if (inTable(Tables.vgic) || inTable(Tables.lgic) || inTable(Tables.other_ic)) {
      IC
    } else if (inTable(Tables.nhr)) {
      NHR
    } else if (inTable(Tables.enzyme)) {
      Enzyme
    } else if (inTable(Tables.catalytic_receptor)) {
      CatalyticReceptor
    } else if (inTable(Tables.transporter)) {
      Transporter
    } else {
      Other
    }
  }
}

# Makes an interaction from raw DB data.
fun makeInteraction(raw) {
  var targetName = switch ((raw.objectName, raw.targetLigandName)) {
    case ([], []) -> ""
    case (x :: _, _) -> x
    case (_, x :: _) -> x
  };

  var i = raw.interaction;
  var interactionType =
    classifyInteractionType(i.object_id, i.target_ligand_id);

  (interactionID = i.interaction_id,
   ligandID = i.ligand_id,
   objectID = i.object_id,
   targetName = targetName,
   interactionType = interactionType,
   interactionAction = i.action,
   interactionActionComment = i.action_comment,
   speciesID = i.species_id,
   endogenous = i.endogenous,
   selective = i.selective,
   useDependent = i.use_dependent,
   voltageDependent = i.voltage_dependent,
   affinity = mkAffinity(i.affinity_units, i.affinity_high, i.affinity_median, i.affinity_low),
   concentrationRange = i.concentration_range,
   affinityVoltage =
     mkAffinityVoltage(i.affinity_voltage_high, i.affinity_voltage_median,
       i.affinity_voltage_low, i.affinity_physiological_voltage),
   rank = i.rank,
   selectivity = i.selectivity,
   originalAffinity =
    mkAffinity(i.original_affinity_units,
      i.original_affinity_high_nm, i.original_affinity_median_nm,
      i.original_affinity_low_nm),
   originalAffinityRelation = i.original_affinity_relation,
   assayDescription = i.assay_description,
   assayConditions = i.assay_conditions,
   fromGrac = i.from_grac,
   onlyGrac = i.only_grac,
   receptorSite = i.receptor_site,
   ligandContext = i.ligand_context,
   percentActivity = i.percent_activity,
   assayURL = i.assay_url,
   primaryTarget = i.primary_target,
   targetLigandID = i.target_ligand_id,
   wholeOrganismAssay = i.whole_organism_assay,
   hide = i.hide,
   references = raw.references)
}

#   original_affinity_low_nm: Float, original_affinity_median_nm: Float, original_affinity_high_nm: Float, original_affinity_units: String, original_affinity_relation: String, assay_description: String, assay_conditions: String, from_grac: Bool, only_grac: Bool, receptor_site: String, ligand_context: String, percent_activity: Float, assay_url: String, primary_target: Bool, target_ligand_id: Int, whole_organism_assay: Bool, hide: Bool)

fun sortAffinity(a1, a2) {
  var a1Key =
    if (a1.affinityLow <> 0.0) {
      a1.affinityLow
    } else {
      a1.affinityMedian
    };

  var a2Key =
    if (a2.affinityLow <> 0.0) {
      a2.affinityLow
    } else {
      a2.affinityMedian
    };

  if (a1Key < a2Key) {
    (-1)
  } else if (a1Key == a2Key) {
    0
  } else {
    1
  }
}

sig displayAffinity : (InteractionAffinity, Bool) ~%~> HTML(a)
fun displayAffinity(af, showUnits) {
  var units =
    if (showUnits) { textNode(" " ^^ af.affinityUnits) } else { MvuHTML.empty };

  # Some interactions do not have affinity data.
  # Others have a range. Others have a median.
  if ((af.affinityLow == 0.0) && (af.affinityMedian == 0.0) && (af.affinityHigh == 0.0)) {
    # No information.
    MvuHTML.empty
  } else if ((af.affinityLow == 0.0) && (af.affinityHigh == 0.0)) {
    # Only a median.
    units +*
    textNode(" ") +*
    textNode(floatToString(af.affinityMedian))
  } else {
    # Range.
    units +*
    textNode(" ") +*
    textNode(floatToString(af.affinityLow) ^^
        " - " ^^ floatToString(af.affinityHigh))
  }
}

fun queryInteractions(pred) {
    query {
      for (i <-- Tables.interaction)
        where (pred(i))
          [(interaction = i,
            ligandName =
              for (l <-- Tables.ligand)
              where (l.ligand_id == i.ligand_id)
              [l.name],
            objectName =
              for (o <-- Tables.object)
              where (o.object_id == i.object_id)
              [o.name],
            targetLigandName =
              for (l <-- Tables.ligand)
              where (l.ligand_id == i.target_ligand_id)
              [l.name],
            references =
              for (r <-- Tables.interaction_affinity_refs)
              where (r.interaction_id == i.interaction_id)
              [r.reference_id])]
    }
}

fun interactionsForLigand(ligandId) {
  queryInteractions(fun(i) { i.ligand_id == ligandId})
}


