# Functionality for dealing with ligand<->object interactions.
# This will grow as I abstract more out.
open import MvuHTML;
open import Parser;
open import Tables;
open import Object;

typename InteractionTargetType = [|
  TargetLigand | Object:ObjectType
|];

typename Target = [| TTargetLigand:RawLigand | TObject:RawObject | TNone |];

fun showInteractionTargetType(it) {
  switch(it) {
    case TargetLigand -> "Target Ligand"
    case Object(ot) -> showObjectType(ot)
  }
}

typename InteractionAffinityVoltage =
  (affinityVoltageHigh: Float,
   affinityVoltageMedian: Float,
   affinityVoltageLow: Float,
   affinityPhysiologicalVoltage: Bool);

typename InteractionAffinity =
  (affinityUnits: String, affinityLow: Float,
   affinityMedian: Float, affinityHigh: Float);

fun mkAffinity(units, high, median, low) {
  (affinityHigh = high, affinityMedian = median, affinityLow = low,
   affinityUnits = units)
}

fun mkAffinityVoltage(high, median, low, physio) {
  (affinityVoltageHigh = high, affinityVoltageMedian = median, affinityVoltageLow = low,
   affinityPhysiologicalVoltage = physio)
}

typename Interaction =
  (interactionID: Int,
   interactionTarget: Target,
   ligandID: Int,
   objectID: Int,
   interactionType: String,
   interactionLigand: RawLigand,
   interactionTargetType: InteractionTargetType,
   interactionAction: String,
   interactionActionComment: String,
   speciesID: Int,
   endogenous: Bool,
   selective: Bool,
   useDependent: Bool,
   voltageDependent: Bool,
   affinity: InteractionAffinity,
   concentrationRange: String,
   affinityVoltage: InteractionAffinityVoltage,
   rank: Int,
   selectivity: String,
   originalAffinity: InteractionAffinity,
   originalAffinityRelation: String,
   assayDescription: String,
   assayConditions: String,
   fromGrac: Bool,
   onlyGrac: Bool,
   receptorSite: String,
   ligandContext: String,
   percentActivity: Float,
   assayURL: String,
   primaryTarget: Bool,
   targetLigandID: Int,
   wholeOrganismAssay: Bool,
   hide: Bool,
   references: [Int]);

fun classifyInteractionType(objectID, targetLigandID) {
  if (targetLigandID > 0) {
    TargetLigand
  } else {
    Object(Object.classifyObject(objectID))
  }
}

# Makes an interaction from raw DB data.
fun makeInteraction(raw) {
  var interactionTarget = switch ((raw.object, raw.targetLigand)) {
    case ([], []) -> TNone
    case (x :: _, _) -> TObject(x)
    case (_, x :: _) -> TTargetLigand(x)
  };

  var i = raw.interaction;
  var interactionTargetType =
    classifyInteractionType(i.object_id, i.target_ligand_id);

  (interactionID = i.interaction_id,
   ligandID = i.ligand_id,
   objectID = i.object_id,
   interactionType = i.type,
   interactionLigand = hd(raw.ligand),
   interactionTarget = interactionTarget,
   interactionTargetType = interactionTargetType,
   interactionAction = i.action,
   interactionActionComment = i.action_comment,
   speciesID = i.species_id,
   endogenous = i.endogenous,
   selective = i.selective,
   useDependent = i.use_dependent,
   voltageDependent = i.voltage_dependent,
   affinity = mkAffinity(i.affinity_units, i.affinity_high, i.affinity_median, i.affinity_low),
   concentrationRange = i.concentration_range,
   affinityVoltage =
     mkAffinityVoltage(i.affinity_voltage_high, i.affinity_voltage_median,
       i.affinity_voltage_low, i.affinity_physiological_voltage),
   rank = i.rank,
   selectivity = i.selectivity,
   originalAffinity =
    mkAffinity(i.original_affinity_units,
      i.original_affinity_high_nm, i.original_affinity_median_nm,
      i.original_affinity_low_nm),
   originalAffinityRelation = i.original_affinity_relation,
   assayDescription = i.assay_description,
   assayConditions = i.assay_conditions,
   fromGrac = i.from_grac,
   onlyGrac = i.only_grac,
   receptorSite = i.receptor_site,
   ligandContext = i.ligand_context,
   percentActivity = i.percent_activity,
   assayURL = i.assay_url,
   primaryTarget = i.primary_target,
   targetLigandID = i.target_ligand_id,
   wholeOrganismAssay = i.whole_organism_assay,
   hide = i.hide,
   references = raw.references)
}

#   original_affinity_low_nm: Float, original_affinity_median_nm: Float, original_affinity_high_nm: Float, original_affinity_units: String, original_affinity_relation: String, assay_description: String, assay_conditions: String, from_grac: Bool, only_grac: Bool, receptor_site: String, ligand_context: String, percent_activity: Float, assay_url: String, primary_target: Bool, target_ligand_id: Int, whole_organism_assay: Bool, hide: Bool)

fun sortAffinity(a1, a2) {
  var a1Key =
    if (a1.affinityLow <> 0.0) {
      a1.affinityLow
    } else {
      a1.affinityMedian
    };

  var a2Key =
    if (a2.affinityLow <> 0.0) {
      a2.affinityLow
    } else {
      a2.affinityMedian
    };

  if (a1Key < a2Key) {
    (-1)
  } else if (a1Key == a2Key) {
    0
  } else {
    1
  }
}

fun sortAffinityVoltage(a1, a2) {
  var a1Key =
    if (a1.affinityVoltageLow <> 0.0) {
      a1.affinityVoltageLow
    } else {
      a1.affinityVoltageMedian
    };

  var a2Key =
    if (a2.affinityVoltageLow <> 0.0) {
      a2.affinityVoltageLow
    } else {
      a2.affinityVoltageMedian
    };

  if (a1Key < a2Key) {
    (-1)
  } else if (a1Key == a2Key) {
    0
  } else {
    1
  }
}
sig displayAffinity : (InteractionAffinity, Bool) ~%~> HTML(a)
fun displayAffinity(af, showUnits) {
  var units =
    if (showUnits) { textNode(" " ^^ af.affinityUnits) } else { MvuHTML.empty };

  # Some interactions do not have affinity data.
  # Others have a range. Others have a median.
  if ((af.affinityLow == 0.0) && (af.affinityMedian == 0.0) && (af.affinityHigh == 0.0)) {
    # No information.
    MvuHTML.empty
  } else if ((af.affinityLow == 0.0) && (af.affinityHigh == 0.0)) {
    # Only a median.
    units +*
    textNode(" ") +*
    textNode(floatToString(af.affinityMedian))
  } else {
    # Range.
    units +*
    textNode(" ") +*
    textNode(floatToString(af.affinityLow) ^^
        " - " ^^ floatToString(af.affinityHigh))
  }
}

fun displayAffinityVoltage(af, showUnits) {
  var units =
    if (showUnits) { textNode(" " ^^ af.affinityVoltageUnits) } else { MvuHTML.empty };
  if ((af.affinityVoltageLow == 0.0) && (af.affinityVoltageMedian == 0.0) && (af.affinityVoltageHigh == 0.0)) {
    # No information.
    MvuHTML.empty
  } else if ((af.affinityVoltageLow == 0.0) && (af.affinityVoltageHigh == 0.0)) {
    # Only a median.
    units +*
    textNode(" ") +*
    textNode(floatToString(af.affinityVoltageMedian))
  } else {
    # Range.
    units +*
    textNode(" ") +*
    textNode(floatToString(af.affinityVoltageLow) ^^
        " - " ^^ floatToString(af.affinityVoltageHigh))
  }
}
fun queryInteractions(pred) {
    query {
      for (i <-- Tables.interaction)
        where (pred(i))
          [(interaction = i,
            ligand =
              for (l <-- Tables.ligand)
              where (l.ligand_id == i.ligand_id)
              [l],
            object =
              for (o <-- Tables.object)
              where (o.object_id == i.object_id)
              [o],
            targetLigand =
              for (l <-- Tables.ligand)
              where (l.ligand_id == i.target_ligand_id)
              [l],
            references =
              for (r <-- Tables.interaction_affinity_refs)
              where (r.interaction_id == i.interaction_id)
              [r.reference_id])]
    }
}

fun interactionsForLigand(ligandId) {
  queryInteractions(fun(i) { i.ligand_id == ligandId})
}

fun interactionsForObject(objectId) {
  queryInteractions(fun(i) { i.object_id == objectId })
}

fun getTargetName(target) {
  switch (target) {
    case TNone -> ""
    case TTargetLigand(tl) -> tl.name
    case TObject(o) -> o.name
  }
}

