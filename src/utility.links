fun listEmpty(xs) { empty(xs) }
fun listSpan(f, xs) { span(f, xs) }
## Useful in defining list predicates in LINQ.
# sig contains : ([a]) ~> ((a) {}-> Bool)
# fun contains(l) {
#   switch (l) {
#     case [] -> fun (x) {false}
#     case x::xs -> fun (y) { x == y || contains(xs)(x) }
#   }
# }
open MvuHTML;
open MvuAttrs;

sig contains : ([a]) ~> ((a) {}-> Bool)
fun contains(xs) {
  fun containsInner(xs, q) {
    switch(xs) {
      case [] -> q
      case x :: xs ->
        containsInner(xs, fun(y) {
          x == y || q(y)
        })
    }
  }

  containsInner(xs, fun(_) { false })
}


sig uniquify : ([a]) ~> [a]
fun uniquify(xs) {
  fun uniqueCons(x, xs) {
    if (elem(x, xs)) {
      xs
    } else {
      x :: xs
    }
  }

  reverse(fold_left(fun(xs, x) { uniqueCons(x, xs) }, [], xs))
}

#sig elemBy : ((a, a) ~> Bool, a, [a]) ~> Bool
fun elemBy(f, x, xs) {
  switch(xs) {
    case [] -> false
    case y :: ys -> if (f(x, y)) { true } else { elemBy(f, x, ys) }
  }
}

#sig uniquifyBy : ((a, a) ~> Bool, [a]) ~> [a]
fun uniquifyBy(f, xs) {

  fun uniqueCons(x, xs) {
    if (elemBy(f, x, xs)) {
      xs
    } else {
      x :: xs
    }
  }
  reverse(fold_left(fun(xs, x) { uniqueCons(x, xs) }, [], xs))
}


fun cmpName(name) {
  # Firstly, strip &..; tags
  var cmpName = (name =~ s/&.*;//) : String;
  dropWhile(fun(c) {
      not(('a' <= c && c <= 'z') || ('A' <= c && c <= 'Z'))
  }, map(toLower, explode(cmpName)))
}

var alphabet = map(fun(c) { chr(c) }, [65..90]);

# Preconditions:
# 1. List is sorted
# 2. The (a) -> Char function returns an alphabetical character
sig alphabetise : (((a) ~%~> [Char]), ((a) ~%~> b), [a]) ~%~> [(Char, [b])]
fun alphabetise(getString, transform, xs) {
  var (chunks, _) =
    fold_left(fun((chunks, remaining), letter) {
        fun safeCmp(exploded) {
          switch(exploded) {
            case [] -> true # Dirty data -- take it anyway
            case x :: _ ->
              x == toLower(letter)
          }
        }
        var (thisChunk, next) = listSpan(fun(x) { safeCmp(getString(x)) }, remaining);
        ((letter, (map(transform, thisChunk))) :: chunks, next)
    }, ([], xs), alphabet);
  reverse(chunks)
}

fun src(s) { MvuAttrs.attr("src", s) }
fun alt(s) { MvuAttrs.attr("alt", s) }

fun boolImage((imgSrc, enabled, altText)) {
  if (enabled) {
    td(class("info") +@
       style("border-style: solid none solid none;padding:1px;text-align:center;vertical-align: middle;"),
      a(style("color:white") +@ title(altText),
        img(src(imgSrc) +@ width("16") +@ height("16") +@ alt(altText), MvuHTML.empty)))
  } else {
    td(class("info") +@
       style("border-style: solid none solid none;padding:1px;text-align:center;vertical-align: middle;"),
       MvuHTML.empty
    )
  }
}

sig partition : ((a) ~b~> Bool, [a]) ~b~> ([a], [a])
fun partition(pred, xs) {
  fun go(ayes, noes, xs) {
    switch(xs) {
      case [] -> (ayes, noes)
      case x :: xs ->
        if (pred(x)) {
          go(x :: ayes, noes, xs)
        } else {
          go(ayes, x :: noes, xs)
        }
    }
  }
  go([], [], xs)
}


# This only works because we have arbitrary polymorphic comparisons.
sig simpleCmp : forall a . (a, a) ~%~> Int
fun simpleCmp(s1, s2) {
  if (s1 < s2) {
    (-1)
  } else if (s1 == s2) {
    0
  } else {
    1
  }
}

sig cmpSort : ((a, a) ~b~> Int, [a]) ~b~> [a]
fun cmpSort(comparator, l) {
  # TBD: rename this 'sortOn' to follow Haskell usage?
  switch (l) {
    case [] -> []
    case x::xs -> {
      var lt = filter (fun (y) {comparator(y, x) < 0 }, xs);
      var ge = filter (fun (y) {comparator(y, x) >= 0 }, xs);
      cmpSort(comparator, lt) ++ [x] ++ cmpSort(comparator, ge)
    }
  }
}
