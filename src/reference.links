# I wonder whether all of this is necessary, or whether we can
# just use the generated DB records directly?
open MvuHTML;
open MvuAttrs;
open Utility;
open Tables;

typename ReferenceType = [| Journal | Other |];

typename Reference = (
  referenceId: Int,
  referenceType: String,
  journalName: String,
  title: String,
  year: Int,
  volume: String,
  issue: String,
  pages: String,
  publisher: String,
  publisher_address: String,
  editors: String,
  pubmedId: Int,
  isbn: String,
  authors: String,
  isKey: Bool # FIXME: This is a horrible hack and should be changed
);

fun stringToRefTy(str) {
  if (str == "Journal") {
    Journal
  } else {
    Other
  }
}
fun refTyToString(refTy) {
  switch(refTy) {
    case Journal -> "Journal"
    case Other -> "Other"
  }
}

fun makeReference(refId,
  refTy, jName, title, y, vol, issue, pgs, publisher,
  pubAddr, editors, pubmed_id, isbn, authors, isKey) {

  (
    referenceId=refId,
    referenceType=refTy, #stringToRefTy(refTy),
    journalName=jName,
    title=title,
    year=y,
    volume=vol,
    issue=issue,
    pages=pgs,
    publisher=publisher,
    publisher_address=pubAddr,
    editors=editors,
    pubmedId=pubmed_id,
    isbn=isbn,
    authors=authors,
    isKey=isKey
  )

}


fun renderCitation(id, refs) {
  var refId = switch(lookup(id, refs)) {
    case Just((refNum, _)) -> intToString(refNum + 1)
    case Nothing -> "?"
  };

  var strId = intToString(id);
  textNode("[") +*
  a(href("#ref" ^^ refId), textNode(refId)) +*
  textNode("]")
}

sig renderCitationList : ([Int], [(Int, (Int, Reference))]) ~> HTML(_)
fun renderCitationList(ids, refs) {
  fun citationLink(id) {
    switch(lookup(id, refs)) {
        case Just((refNum, _)) ->
          var refId = intToString(refNum + 1);
          a(href("#ref" ^^ refId), textNode(refId))
        case Nothing -> textNode("?")
    }
  }

  if (length(ids) > 0) {
    var links = map(fun(i) { [citationLink(i)] }, ids);
    var separatedLinks =
      MvuHTML.concat(join([textNode(", ")], links));
    textNode("[") +* separatedLinks +* textNode("]")
  } else {
    MvuHTML.empty
  }
}

fun queryReferences(refIds) {
  var containsReference = contains(refIds);
  query {
  for (ref <-- Tables.reference)
    where (containsReference(ref.reference_id))
      [
        (makeReference(ref.reference_id,
             ref.type, ref.title, ref.article_title, ref.year,
             ref.volume, ref.issue, ref.pages, ref.publisher,
             ref.publisher_address, ref.editors, ref.pubmed_id,
             ref.isbn, ref.authors, false))
      ]
    }
}
